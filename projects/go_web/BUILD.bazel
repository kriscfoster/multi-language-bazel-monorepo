load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball", "oci_image_index", "oci_push")
load("@bazoku//:bazoku.bzl", "bazoku")

go_library(
    name = "go_web_lib",
    srcs = ["main.go"],
    importpath = "github.com/kriscfoster/multi-language-bazel-monorepo/projects/go_web",
    visibility = ["//visibility:private"],
    deps = [
        "//projects/go_hello_world",
        "@com_github_gorilla_mux//:mux",
    ],
)

go_binary(
    name = "go_web",
    embed = [":go_web_lib"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar",
    srcs = [":go_web"],
)

oci_image(
    name = "oci_image",
    base = "@distroless_base",
    # with :debug distroless image, entrypoint can be changed to "sh" for debug purposes
    entrypoint = ["go_web_/go_web"],
    tars = [":tar"],
)

archs = ["arm64", "amd64"]

[
    go_binary(
        name = "go_web_" + arch,
        embed = [":go_web_lib"],
        visibility = ["//visibility:public"],
        goos = "linux",
        goarch = arch,
    )
    for arch in archs
]

[
    pkg_tar(
        name = "tar_" + arch,
        srcs = [":go_web_" + arch],
    )
    for arch in archs
]

[
    oci_image(
        name = "oci_image_" + arch,
        base = "@distroless_base_linux_" + arch,
        # with :debug distroless image, entrypoint can be changed to "sh" for debug purposes
        entrypoint = ["go_web_" + arch + "_/go_web_" + arch],
        tars = [":tar_" + arch],
    )
    for arch in archs
]

oci_image_index(
    name = "multi_arch_image",
    images = [
        ":oci_image_arm64",
        ":oci_image_amd64"
    ],
)

oci_push(
    name = "publish",
    repository = "registry.hub.docker.com/krisfoster96/monorepo-go-web",
    remote_tags = ["6"],
    image = ":multi_arch_image",
)

oci_tarball(
    name = "oci_tarball_amd64",
    image = ":oci_image_amd64",
    repo_tags = ["projects/go_web:oci_tarball_amd64"],
)

bazoku(
    name = "bazoku-deployment",
    binary = ":go_web",
    heroku_app_name = "my-bazoku-app",
)
