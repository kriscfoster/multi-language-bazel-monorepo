load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_push", "oci_tarball")
load("@bazoku//:bazoku.bzl", "bazoku")

go_library(
    name = "go_web_lib",
    srcs = ["main.go"],
    importpath = "github.com/kriscfoster/multi-language-bazel-monorepo/projects/go_web",
    visibility = ["//visibility:private"],
    deps = [
        "//projects/go_hello_world",
        "@com_github_gorilla_mux//:mux",
    ],
)

archs = [
    "arm64",
    "amd64",
]

[
    go_binary(
        name = "go_web_" + arch,
        embed = [":go_web_lib"],
        goarch = arch,
        goos = "linux",
        visibility = ["//visibility:public"],
    )
    for arch in archs
]

[
    pkg_tar(
        name = "tar_" + arch,
        srcs = [":go_web_" + arch],
    )
    for arch in archs
]

oci_image(
    name = "oci_image_amd64",
    base = "@distroless_base_linux_amd64",
    # with :debug distroless image, entrypoint can be changed to "sh" for debug purposes
    entrypoint = ["go_web_amd64_/go_web_amd64"],
    tars = [":tar_amd64"],
)

oci_image(
    name = "oci_image_arm64",
    base = "@distroless_base_linux_arm64_v8",
    # with :debug distroless image, entrypoint can be changed to "sh" for debug purposes
    entrypoint = ["go_web_arm64_/go_web_arm64"],
    tars = [":tar_arm64"],
)

[
    oci_tarball(
        name = "oci_tarball_" + arch,
        image = ":oci_image_" + arch,
        repo_tags = ["projects/go_web:oci_tarball_" + arch],
    )
    for arch in archs
]

oci_image_index(
    name = "multi_arch_image",
    images = [
        ":oci_image_arm64",
        ":oci_image_amd64",
    ],
)

oci_push(
    name = "publish",
    image = ":multi_arch_image",
    remote_tags = ["6"],
    repository = "registry.hub.docker.com/krisfoster96/monorepo-go-web",
)

bazoku(
    name = "bazoku-deployment",
    binary = ":go_web_amd64",
    heroku_app_name = "my-bazoku-app",
)

go_binary(
    name = "go_web",
    embed = [":go_web_lib"],
    visibility = ["//visibility:public"],
)
